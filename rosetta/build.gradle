apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'maven-publish'

// version information
def versionMajor = 1 // major
def versionMinor = 2 // minor
def versionPatch = 1 // revision

ext {
    version_name = "${versionMajor}.${versionMinor}.${versionPatch}"
    artifact_name = 'rosetta-rosetta'
}

def isReleaseBuild() {
    return !ext.version_name.contains("SNAPSHOT")
}

def getAWSUrl() {
    if (isReleaseBuild()) {
        return AWS_URL
    } else {
        return AWS_URL_SNAPSHOT
    }
}

def getArtifactFilePath() {
    if (isReleaseBuild()) {
        return "$buildDir/outputs/aar/$artifact_name-release.aar"
    } else {
        return "$buildDir/outputs/aar/$artifact_name-debug.aar"
    }
}

buildscript {
    repositories {
        jcenter()
        mavenCentral()
        google()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.2.1'
    }
}

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 28
        archivesBaseName = "${artifact_name}"
        versionCode versionMajor * 1000 + versionMinor * 100 + versionPatch
        versionName "${version_name}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            java.srcDirs = ['src']
            manifest.srcFile 'src/main/AndroidManifest.xml'
        }
    }

    repositories {
        maven { url 'http://repo1.maven.org/maven2' }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            version = "${version_name}"
            groupId 'com.docutools'
            artifactId "${artifact_name}"
            artifact getArtifactFilePath()
        }
    }
    repositories {
        maven {
            url getAWSUrl()
            credentials (AwsCredentials) {
                accessKey AWS_ACCESS_KEY
                secretKey AWS_SECRET_KEY
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation 'junit:junit:4.12'
    implementation 'org.mockito:mockito-core:1.10.19'
    implementation 'com.android.support:appcompat-v7:28.0.0'
}